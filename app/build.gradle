buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    useLibrary 'org.apache.http.legacy'
    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    defaultConfig {
        applicationId "com.remotedev.boilerplate"
        minSdkVersion 19
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        versionName '1.0.0'
        versionCode 1
    }
    buildTypes {
        debug {
            // signing, buildname/version and buildconfigs for Debug buildtype
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "RemoteDev Boilerplate (DEBUG)"
            buildConfigField "com.remotedev.boilerplate.enums.BuildEnum",
                    "BUILD", "com.remotedev.boilerplate.enums.BuildEnum.DEBUG"

            // disable crashlytics for debug to improve building speed
            ext.enableCrashlytics = false
        }
        alpha {
            // signing, buildname/version and buildconfigs for Alpha buildtype
            applicationIdSuffix '.alpha'
            versionNameSuffix '-ALPHA'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "RemoteDev Boilerplate (Alpha)"
            buildConfigField "com.remotedev.boilerplate.enums.BuildEnum",
                    "BUILD", "com.remotedev.boilerplate.enums.BuildEnum.ALPHA"
        }
        beta {
            // signing, buildname/version and buildconfigs for Beta buildtype
            applicationIdSuffix '.beta'
            versionNameSuffix '-BETA'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "RemoteDev Boilerplate (Beta)"
            buildConfigField "boolean", "ERASEPREFERENCES", "false"
            buildConfigField "com.remotedev.boilerplate.enums.BuildEnum",
                    "BUILD", "com.remotedev.boilerplate.enums.BuildEnum.BETA"
        }
        release {
            // signing, buildname/version and buildconfigs for Release buildtype
            signingConfig signingConfigs.release
            resValue "string", "app_name", "RemoteDev Boilerplate"
            buildConfigField "com.remotedev.boilerplate.enums.BuildEnum",
                    "BUILD", "com.remotedev.boilerplate.enums.BuildEnum.RELEASE"

            // proguard/compression settings
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            minifyEnabled true
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    implementation 'com.daimajia.easing:library:2.0@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.android.support:customtabs:28.0.0'
    implementation "de.adorsys.android:securestoragelibrary:1.0.2"
    implementation 'com.android.support:support-v4:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true;
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true;
    }
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
